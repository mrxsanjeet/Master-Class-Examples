OBJECT Codeunit 50008 Ex. Jnl.-B.Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=12-03-14;
    Time=19:58:45;
    Modified=Yes;
    Version List=Exercise 3;
  }
  PROPERTIES
  {
    TableNo=50014;
    OnRun=BEGIN
            ExJnlBatch.COPY(Rec);
            Code;
            Rec := ExJnlBatch;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to post the journals and print the posting report?;ESM=Confirma que desea registrar los diarios e imprimir el informe de registro?;FRC=Dsirez-vous reporter le journal et imprimer le rapport?;ENC=Do you want to post the journals and print the posting report?';
      Text001@1001 : TextConst 'ENU=The journals were successfully posted.;ESM=Se han registrado correctamente los diarios.;FRC=Le journal a t report avec succs.;ENC=The journals were successfully posted.';
      Text002@1002 : TextConst 'ENU="It was not possible to post all of the journals. ";ESM="No ha sido posible registrar todos los diarios. ";FRC="Il a t impossible de reporter tous les journaux. ";ENC="It was not possible to post all of the journals. "';
      Text003@1003 : TextConst 'ENU=The journals that were not successfully posted are now marked.;ESM=Los diarios que no se han registrado estn marcados.;FRC=Les journaux qui n''ont pas t reports sont maintenant indiqus.;ENC=The journals that were not successfully posted are now marked.';
      ExJnlTemplate@1004 : Record 50012;
      ExJnlBatch@1005 : Record 50014;
      ExJnlLine@1006 : Record 50013;
      ExReg@1007 : Record 50015;
      ExJnlPostBatch@1008 : Codeunit 50004;
      JnlWithErrors@1009 : Boolean;

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH ExJnlBatch DO BEGIN
        ExJnlTemplate.GET("Journal Template Name");
        ExJnlTemplate.TESTFIELD("Posting Report ID");

        IF NOT CONFIRM(Text000) THEN
          EXIT;

        FIND('-');
        REPEAT
          ExJnlLine."Journal Template Name" := "Journal Template Name";
          ExJnlLine."Journal Batch Name" := Name;
          ExJnlLine."Line No." := 1;
          CLEAR(ExJnlPostBatch);
          IF ExJnlPostBatch.RUN(ExJnlLine) THEN BEGIN
            MARK(FALSE);
            IF ExReg.GET(ExJnlLine."Line No.") THEN BEGIN
              ExReg.SETRECFILTER;
              REPORT.RUN(ExJnlTemplate."Posting Report ID",FALSE,FALSE,ExReg);
            END;
          END ELSE BEGIN
            MARK(TRUE);
            JnlWithErrors := TRUE;
          END;
        UNTIL NEXT = 0;

        IF NOT JnlWithErrors THEN
          MESSAGE(Text001)
        ELSE
          MESSAGE(
            Text002 +
            Text003);

        IF NOT FIND('=><') THEN BEGIN
          RESET;
          Name := '';
        END;
      END;
    END;

    BEGIN
    END.
  }
}

